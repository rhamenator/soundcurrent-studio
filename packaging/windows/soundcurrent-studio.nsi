; SPDX-License-Identifier: GPL-3.0-only
Unicode true
!include "MUI2.nsh"
!include "nsDialogs.nsh"
!include "LogicLib.nsh"
!include "FileFunc.nsh"

!ifndef APP_EXE
  !error "Pass /DAPP_EXE=path-to-soundcurrent-studio.exe"
!endif
!ifndef OUTPUT
  !error "Pass /DOUTPUT=path-to-installer.exe"
!endif
!ifndef SOURCE_ROOT
  !error "Pass /DSOURCE_ROOT=path-to-repository"
!endif
!ifndef CABLE_ZIP
  !error "Pass /DCABLE_ZIP=path-to-verified-VBCABLE_Driver_Pack45.zip"
!endif

!ifndef APP_VERSION
  !define APP_VERSION "0.8.3"
!endif

Var CableCheck
Var CableChoice
Var InstallCable

Name "SoundCurrent Studio"
OutFile "${OUTPUT}"
InstallDir "$LOCALAPPDATA\Programs\SoundCurrent Studio"
InstallDirRegKey HKCU "Software\SoundCurrent\SoundCurrent Studio" "InstallDir"
RequestExecutionLevel user
SetCompressor /SOLID lzma
BrandingText "SoundCurrent Studio • GPL-3.0-only"
Icon "${SOURCE_ROOT}\data\soundcurrent-studio.ico"
UninstallIcon "${SOURCE_ROOT}\data\soundcurrent-studio.ico"

!define MUI_WELCOMEPAGE_TEXT "Install or update SoundCurrent Studio. You do not need to uninstall an older version. Your settings, presets and equipment profiles will be kept.$\r$\n$\r$\nSave your work and use Quit to exit the running app before continuing. Closing its window keeps it running in the background."
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${SOURCE_ROOT}\LICENSE"
!insertmacro MUI_PAGE_DIRECTORY
Page custom AudioPage AudioPageLeave
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_REBOOTLATER_DEFAULT
!define MUI_FINISHPAGE_RUN "$INSTDIR\soundcurrent-studio.exe"
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

Function .onInit
  ; Migrate the old install location, including custom folders.
  ReadRegStr $1 HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "DisplayIcon"
  ${If} $1 != ""
    ${GetParent} $1 $2
    IfFileExists "$2\soundcurrent-studio.exe" 0 +2
      StrCpy $INSTDIR $2
  ${EndIf}
  StrCpy $InstallCable 0 ; Silent app updates never install/elevate a driver.
  InitPluginsDir
  SetOutPath "$PLUGINSDIR"
  File "/oname=audio-setup.ps1" "${SOURCE_ROOT}\packaging\windows\audio-setup.ps1"
  nsExec::ExecToStack /TIMEOUT=20000 '"$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" -NoProfile -NonInteractive -ExecutionPolicy RemoteSigned -File "$PLUGINSDIR\audio-setup.ps1" -Check'
  Pop $CableCheck
  Pop $0
FunctionEnd

Function AudioPage
  !insertmacro MUI_HEADER_TEXT "Connect your audio" "Set up the virtual cable used by SoundCurrent Studio."
  nsDialogs::Create 1018
  Pop $0
  ${If} $0 == error
    Abort
  ${EndIf}
  ${NSD_CreateLabel} 0 0 100% 25u "VB-CABLE connects Windows playback to the equalizer. Your speakers or headphones remain the physical output."
  Pop $0
  ${NSD_CreateCheckbox} 0 30u 100% 15u "Install the standard VB-CABLE driver"
  Pop $CableChoice
  ${If} $CableCheck == 10
    ${NSD_Check} $CableChoice
    ${NSD_CreateLabel} 0 50u 100% 28u "Windows will ask for administrator approval. In VB-Audio's setup, click Install Driver. Restart Windows afterward."
  ${ElseIf} $CableCheck == 0
    EnableWindow $CableChoice 0
    ${NSD_CreateLabel} 0 50u 100% 28u "VB-CABLE is already installed. Setup will keep the existing driver."
  ${Else}
    EnableWindow $CableChoice 0
    ${NSD_CreateLabel} 0 50u 100% 28u "Setup could not check for an existing driver. After setup, use the Install VB-CABLE shortcut in the Start menu to retry."
  ${EndIf}
  Pop $0
  ${NSD_CreateLabel} 0 84u 100% 30u "VB-CABLE is separate VB-Audio software under its own donationware terms. If useful, donate/pay for a license. Professional deployments may require paid licenses."
  Pop $0
  ${NSD_CreateButton} 0 117u 48% 17u "VB-CABLE website / donations"
  Pop $0
  ${NSD_OnClick} $0 CableWebsite
  ${NSD_CreateButton} 52% 117u 48% 17u "VB-Audio licensing terms"
  Pop $0
  ${NSD_OnClick} $0 CableLicense
  nsDialogs::Show
FunctionEnd

Function CableWebsite
  Pop $0
  ExecShell "open" "https://www.vb-cable.com/"
FunctionEnd
Function CableLicense
  Pop $0
  ExecShell "open" "https://vb-audio.com/Services/licensing.htm"
FunctionEnd
Function AudioPageLeave
  ${NSD_GetState} $CableChoice $InstallCable
FunctionEnd

Section "SoundCurrent Studio" main
  FindWindow $0 "" "SoundCurrent Studio"
  StrCmp $0 0 +3
    MessageBox MB_ICONEXCLAMATION "Quit SoundCurrent Studio before updating. Closing the window keeps it running. No uninstall is needed."
    Abort
  SetOutPath "$INSTDIR"
!ifdef DLL_DIR
  File /r "${DLL_DIR}\*"
!else
  File "/oname=soundcurrent-studio.exe" "${APP_EXE}"
!endif
  File "${SOURCE_ROOT}\LICENSE"
  File "${SOURCE_ROOT}\COPYRIGHT"
  File "${SOURCE_ROOT}\README.md"
  File "${SOURCE_ROOT}\packaging\windows\audio-setup.ps1"
  File "/oname=VBCABLE_Driver_Pack45.zip" "${CABLE_ZIP}"
  File "${SOURCE_ROOT}\packaging\windows\VB-CABLE-NOTICE.txt"
  WriteRegStr HKCU "Software\SoundCurrent\SoundCurrent Studio" "InstallDir" "$INSTDIR"
  WriteUninstaller "$INSTDIR\uninstall.exe"
  CreateDirectory "$SMPROGRAMS\SoundCurrent Studio"
  CreateShortcut "$SMPROGRAMS\SoundCurrent Studio\SoundCurrent Studio.lnk" "$INSTDIR\soundcurrent-studio.exe"
  CreateShortcut "$SMPROGRAMS\SoundCurrent Studio\Uninstall.lnk" "$INSTDIR\uninstall.exe"
  CreateShortcut "$SMPROGRAMS\SoundCurrent Studio\Install VB-CABLE.lnk" "$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" '-NoProfile -ExecutionPolicy RemoteSigned -File "$INSTDIR\audio-setup.ps1" -Install' "$INSTDIR\soundcurrent-studio.exe"
  CreateShortcut "$DESKTOP\SoundCurrent Studio.lnk" "$INSTDIR\soundcurrent-studio.exe"
  WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "DisplayName" "SoundCurrent Studio"
  WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "DisplayIcon" "$INSTDIR\soundcurrent-studio.exe"
  WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "Publisher" "SoundCurrent Studio contributors"
  WriteRegStr HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "DisplayVersion" "${APP_VERSION}"
  WriteRegDWORD HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "NoModify" 1
  WriteRegDWORD HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio" "NoRepair" 1
  ${If} $InstallCable == ${BST_CHECKED}
    DetailPrint "Opening VB-Audio's signed driver installer..."
    nsExec::ExecToStack '"$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" -NoProfile -ExecutionPolicy RemoteSigned -File "$INSTDIR\audio-setup.ps1" -Install -Quiet'
    Pop $0
    Pop $1
    DetailPrint $1
    ${If} $0 == 3010
      SetRebootFlag true
    ${ElseIf} $0 != 0
      DetailPrint "VB-CABLE setup did not finish. Retry using the Start menu shortcut."
      MessageBox MB_OK|MB_ICONINFORMATION "VB-CABLE was not installed. SoundCurrent Studio itself is installed. Use Install VB-CABLE in the Start menu to retry; see setup details for the reason."
    ${EndIf}
  ${EndIf}
SectionEnd

Section "Uninstall"
  FindWindow $0 "" "SoundCurrent Studio"
  StrCmp $0 0 +3
    MessageBox MB_ICONEXCLAMATION "Quit SoundCurrent Studio before uninstalling it."
    Abort
  Delete "$DESKTOP\SoundCurrent Studio.lnk"
  Delete "$SMPROGRAMS\SoundCurrent Studio\SoundCurrent Studio.lnk"
  Delete "$SMPROGRAMS\SoundCurrent Studio\Uninstall.lnk"
  Delete "$SMPROGRAMS\SoundCurrent Studio\Install VB-CABLE.lnk"
  RMDir "$SMPROGRAMS\SoundCurrent Studio"
!ifdef UNINSTALL_PAYLOAD
  !include "${UNINSTALL_PAYLOAD}"
!else
  Delete "$INSTDIR\soundcurrent-studio.exe"
!endif
  Delete "$INSTDIR\LICENSE"
  Delete "$INSTDIR\COPYRIGHT"
  Delete "$INSTDIR\README.md"
  Delete "$INSTDIR\audio-setup.ps1"
  Delete "$INSTDIR\VBCABLE_Driver_Pack45.zip"
  Delete "$INSTDIR\VB-CABLE-NOTICE.txt"
  Delete "$INSTDIR\uninstall.exe"
  RMDir "$INSTDIR"
  DeleteRegKey HKCU "Software\Microsoft\Windows\CurrentVersion\Uninstall\SoundCurrentStudio"
SectionEnd
